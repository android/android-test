# Description: Tests for Runner

load(
    "//build_extensions:android_library_instrumentation_tests.bzl",
    "android_library_instrumentation_tests",
)
load(
    "//build_extensions:test_devices.bzl",
    "devices",
)

package(
    default_testonly = 1,
)

licenses(["notice"])  # Apache License 2.0

android_library(
    name = "legacy_test_manifest",
    exports_manifest = 1,
    manifest = "AndroidManifest.xml",
)

PARAMETERIZED_SRCS = [
    "internal/runner/AndroidLogOnlyBuilderTest.java",
    "internal/runner/TestLoaderTest.java",
]

UI_THREAD_STATEMENT_TEST_EXAMPLES = [
    "internal/runner/junit4/statement/UiThreadTestInterface.java",
    "internal/runner/junit4/statement/UiThreadTestOnClass.java",
    "internal/runner/junit4/statement/UiThreadTestOnImplementedInterface.java",
    "internal/runner/junit4/statement/UiThreadTestOnMethod.java",
    "internal/runner/junit4/statement/UiThreadTestOnSuperclass.java",
]

# TODO(b/26110951) - remove when these classes are standalone parameterized tests
android_library(
    name = "parameterized",
    srcs = PARAMETERIZED_SRCS,
    deps = [
        "//ext/junit",
        "//runner/android_junit_runner",
        "//runner/android_junit_runner/javatests/androidx/test/testing/fixtures",
        "@maven//:junit_junit",
        "@maven//:org_hamcrest_hamcrest_all",
        "@maven//:org_mockito_mockito_core",
    ],
)

android_library(
    name = "ui_thread_statement_test_examples",
    srcs = UI_THREAD_STATEMENT_TEST_EXAMPLES,
    deps = [
        "//ext/junit",
        "//runner/rules",
        "@maven//:junit_junit",
    ],
)

# test classpath scanning for a multidex test apk, with a small app apk
android_binary(
    name = "stub_target_app",
    manifest = "//build_extensions:AndroidManifest_target_stub.xml",
    manifest_values = {
        "applicationId": "androidx.test.multidex",
    },
    deps = [
        "//runner/android_junit_runner/javatests/androidx/test/testing/fixtures",
    ],
)

android_binary(
    name = "ClassPathScannerTest_app",
    srcs = ["internal/runner/ClassPathScannerTest.java"],
    instruments = ":stub_target_app",
    manifest = "//build_extensions:AndroidManifest_instrumentation_test_template.xml",
    manifest_values = {
        "applicationId": "androidx.test.tests",
        "instrumentationTargetPackage": "androidx.test.multidex",
    },
    multidex = "legacy",
    deps = [
        # include the large number of methods that will force classes off main dex
        "//testapps/multidex_testapp/java/androidx/test/multidex/app:src",
        "//ext/junit",
        "//runner/android_junit_runner",
        "@maven//:junit_junit",
        "@maven//:com_google_truth_truth",
    ],
)

android_instrumentation_test(
    name = "ClassPathScannerTest",
    args = [
        "--test_class=androidx.test.internal.runner.ClassPathScannerTest",
        "--test_filter_spec",
        "-TEST_NAME",
        "--test_package_names",
        "androidx.test.testing",
    ],
    target_device = "//tools/android/emulated_devices/generic_phone:android_19_x86",
    test_app = ":ClassPathScannerTest_app",
)

android_library_instrumentation_tests(
    name = "android_junit_runner_tests",
    size = "large",
    srcs = glob(
        ["**/*.java"],
        exclude = [
            # suites not supported in Google3
            "JUnitTestSuiteTest.java",
            "MyTestSuiteBuilder.java",
            # performance/profiling test
            "JUnit3HogTest.java",
            "JUnit4HogTest.java",
            # empty tests not supported
            "MyEmptyTest.java",
            # parameterized tests not supported
            "MyParameterizedTest.java",
            # suppressed
            "TheoriesAndTestsTogether.java",
            # tested separately
            "internal/runner/ClassPathScannerTest.java",
        ] + PARAMETERIZED_SRCS + UI_THREAD_STATEMENT_TEST_EXAMPLES,
    ),
    args = [
        "--test_filter_spec",
        "-TEST_NAME",
        "--test_package_names",
        "androidx.test.testing",
    ],
    target_devices = devices(),
    deps = [
        ":legacy_test_manifest",
        ":parameterized",
        ":ui_thread_statement_test_examples",
        "//core",
        "//ext/junit",
        "//runner/android_junit_runner",
        "//runner/android_junit_runner/javatests/androidx/test/testing/fixtures",
        "//runner/monitor",
        "//runner/rules",
        "//services/events/java/androidx/test/services/events",
        "@androidsdk//:legacy_test-30",
        "@maven//:com_google_truth_truth",
        "@maven//:com_linkedin_dexmaker_dexmaker",
        "@maven//:com_linkedin_dexmaker_dexmaker_mockito",
        "@maven//:junit_junit",
        "@maven//:org_hamcrest_hamcrest_all",
        "@maven//:org_mockito_mockito_core",
        "@maven//:org_objenesis_objenesis",
    ],
)
