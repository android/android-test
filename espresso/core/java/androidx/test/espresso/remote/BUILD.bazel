# Remote package for espresso.

licenses(["notice"])  # Apache License 2.0

package(
    default_visibility = [
        "//visibility:public",
    ],
)

REMOTE_INTERFACES = [
    "NoopRemoteInteraction.java",
    "RemoteInteraction.java",
    "RemoteInteractionRegistry.java",
    "EspressoRemoteMessage.java",
]

REMOTE_REGISTRY = [
    "RemoteDescriptorRegistry.java",
    "RemoteDescriptor.java",
    "FieldDescriptor.java",
]

android_library(
    name = "registry",
    srcs = REMOTE_REGISTRY,
    visibility = ["//visibility:public"],
    deps = [
        ":exceptions",
        ":reflectionUtils",
        "//:androidx_annotation",
        "//espresso/core/java/androidx/test/espresso/proto:ui_interaction_android_pb3",
        "//espresso/core/java/androidx/test/espresso/remote/annotation:remote_msg_annotations",
        "@com_google_protobuf//:protobuf_javalite",
        "@maven//:com_google_guava_guava",
    ],
)

android_library(
    name = "interface",
    srcs = REMOTE_INTERFACES,
    visibility = ["//visibility:public"],
    deps = [
        ":exceptions",
        ":reflectionUtils",
        "//:androidx_annotation",
        "//espresso/core/java/androidx/test/espresso:framework",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/remote/annotation:remote_msg_annotations",
        "@com_google_protobuf//:protobuf_javalite",
        "@maven//:com_google_guava_guava",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

REFLECTION_UTILS = [
    "MethodInvocation.java",
    "ConstructorInvocation.java",
]

android_library(
    name = "reflectionUtils",
    srcs = REFLECTION_UTILS,
    visibility = ["//visibility:public"],
    deps = [
        ":exceptions",
        "//:androidx_annotation",
        "//runner/android_junit_runner/java/androidx/test:runner",
        "@com_google_protobuf//:protobuf_javalite",
        "@maven//:com_google_guava_guava",
    ],
)

PROTO_UTILS = [
    "Converter.java",
    "AnyToTypeConverter.java",
    "TypeToAnyConverter.java",
    "ByteStringToTypeConverter.java",
    "TypeToByteStringConverter.java",
    "ParcelableToByteStringConverter.java",
    "ByteStringToParcelableConverter.java",
    "TypeProtoConverters.java",
    "ProtoUtils.java",
    "InteractionRequest.java",
    "GenericRemoteMessage.java",
    "RemoteMessageSerializer.java",
    "RemoteMessageDeserializer.java",
    "BuilderReflector.java",
    "ProtoReflector.java",
]

android_library(
    name = "aidls",
    custom_package = "androidx.test.espresso.remote",
    idl_import_root = "idls",
    idl_srcs = glob(["idls/**"]),
    visibility = ["//visibility:public"],
    deps = ["@com_google_protobuf//:protobuf_javalite"],
)

android_library(
    name = "protoUtils",
    srcs = PROTO_UTILS,
    visibility = ["//visibility:public"],
    deps = [
        ":aidls",
        ":exceptions",
        ":interface",
        ":reflectionUtils",
        ":registry",
        "//:androidx_annotation",
        "//espresso/core/java/androidx/test/espresso:framework",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/proto:ui_interaction_android_pb3",
        "//espresso/core/java/androidx/test/espresso/remote/annotation:remote_msg_annotations",
        "//runner/android_junit_runner/java/androidx/test:runner",
        "@com_google_protobuf//:protobuf_javalite",
        "@maven//:com_google_guava_guava",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

REMOTE_EXCEPTIONS = [
    "RemoteEspressoException.java",
    "RemoteProtocolException.java",
    "NoRemoteEspressoInstanceException.java",
]

android_library(
    name = "exceptions",
    srcs = REMOTE_EXCEPTIONS,
    visibility = ["//visibility:public"],
    deps = [
        "//espresso/core/java/androidx/test/espresso:interface",
        "@com_google_protobuf//:protobuf_javalite",
    ],
)

BINDABLE = [
    "Bindable.java",
]

android_library(
    name = "bindable",
    srcs = BINDABLE,
    deps = ["@com_google_protobuf//:protobuf_javalite"],
)

android_library(
    name = "remote",
    srcs = glob(
        ["*.java"],
        exclude = REMOTE_INTERFACES + PROTO_UTILS + REMOTE_EXCEPTIONS + BINDABLE,
    ),
    deps = [
        ":aidls",
        ":exceptions",
        ":interface",
        ":protoUtils",
        "//:androidx_annotation",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso:data-interaction-remote",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/action:remote_view_actions",
        "//espresso/core/java/androidx/test/espresso/assertion:remote_view_assertions",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "//espresso/core/java/androidx/test/espresso/matcher:remote_hamcrest_v1_3_core_matchers",
        "//espresso/core/java/androidx/test/espresso/matcher:remote_view_matchers",
        "//espresso/core/java/androidx/test/espresso/proto:ui_interaction_android_pb3",
        "//espresso/core/java/androidx/test/espresso/proto/matcher:ui_interaction_view_matchers_android_pb3",
        "//espresso/core/java/androidx/test/espresso/remote:bindable",
        "//espresso/core/java/androidx/test/espresso/remote/annotation:remote_msg_annotations",
        "//espresso/web/java/androidx/test/espresso/web/action:remote_web_actions",
        "//espresso/web/java/androidx/test/espresso/web/assertion:remote_web_assertions",
        "//espresso/web/java/androidx/test/espresso/web/matcher:remote_web_matchers",
        "//espresso/web/java/androidx/test/espresso/web/model:remote_web_model_atoms",
        "//espresso/web/java/androidx/test/espresso/web/sugar:remote_sugar",
        "//espresso/web/java/androidx/test/espresso/web/webdriver:remote_webdriver_atoms",
        "//runner/android_junit_runner",
        "//runner/monitor",
        "@com_google_protobuf//:protobuf_javalite",
        "@maven//:com_google_guava_guava",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

load("//build_extensions:maven_repo.bzl", "maven_artifact")
load("//build_extensions:axt_versions.bzl", "RUNNER_VERSION", "ESPRESSO_VERSION", "ANDROIDX_VERSION")
load("//build_extensions:combine_jars.bzl", "combine_jars")

filegroup(
    name = "espresso_remote_src",
    srcs = [
        ":libbindable-src.jar",
        ":libexceptions-src.jar",
        ":libinterface-src.jar",
        ":libprotoUtils-src.jar",
        ":libreflectionUtils-src.jar",
        ":libregistry-src.jar",
        ":libremote-src.jar",
    ],
)

combine_jars(
    name = "espresso_remote_src_jars",
    srcs = [
        ":espresso_remote_src",
    ],
)

load("//build_extensions:release.bzl", "axt_release_lib")

axt_release_lib(
    name = "espresso_remote_release",
    jarjar_rules = "release_jarjar_rules.txt",
    keep_spec = "androidx/test/espresso",
    overlapping_jars = [
        "//espresso/core/java/androidx/test/espresso:espresso_core_release_no_deps.jar",
        "//espresso/web/java/androidx/test/espresso/web:espresso_web_release_no_deps.jar",
        "//espresso/idling_resource/java/androidx/test/espresso:espresso_idling_resource_release_no_deps.jar",
    ],
    proguard_library = ":proguard_library.cfg",
    proguard_specs = [
        "//:proguard_binary.cfg",
        "//espresso:proguard_binary.cfg",
    ],
    deps = [
        ":remote",
    ],
)

maven_artifact(
    name = "espresso_remote_maven_artifact",
    src = ":espresso_remote_release.aar",
    artifact_deps = [
        "androidx.test:runner:%s" % RUNNER_VERSION,
        "androidx.test.espresso:espresso-core:%s" % ESPRESSO_VERSION,
        "androidx.test.espresso:espresso-web:%s" % ESPRESSO_VERSION,
        "androidx.annotation:annotation:%s" % ANDROIDX_VERSION,
    ],
    artifact_id = "espresso-remote",
    group_id = "androidx.test.espresso",
    last_updated = "20170622000000",
    src_jar = ":espresso_remote_src_jars.jar",
    version = "%s" % ESPRESSO_VERSION,
)
