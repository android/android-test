# Description: Tests for espresso contrib
licenses(["notice"])  # Apache License 2.0

android_library(
    name = "espresso_contrib_test_lib",
    testonly = 1,
    # TODO(b/119838560): DrawerActionsIntegrationTest and NavigationViewActionsIntegrationTest fail on bazel
    srcs = glob(["**/*.java"], exclude = ["DrawerActionsIntegrationTest.java", "NavigationViewActionsIntegrationTest.java"]),
    manifest = "AndroidManifest.xml",
    deps = [
        "//:androidx_core",
        "//:androidx_legacy_support_v4",
        "//:androidx_recyclerview",
        "//core",
        "//espresso/contrib/java/androidx/test/espresso/contrib:accessibility_checks",
        "//espresso/contrib/java/androidx/test/espresso/contrib:activity_result_matchers",
        "//espresso/contrib/java/androidx/test/espresso/contrib:design_lib_actions",
        "//espresso/contrib/java/androidx/test/espresso/contrib:drawer_actions",
        "//espresso/contrib/java/androidx/test/espresso/contrib:recycler_view",
        "//espresso/contrib/java/androidx/test/espresso/contrib:view_pager_actions",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso:framework",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "//espresso/idling_resource/net/java/androidx/test/espresso/idling/net:uri_idling_resource",
        "//ext/junit",
        "//runner/android_junit_runner",
        "//runner/rules",
        "//testapps/ui_testapp/java/androidx/test/ui/app:lib_neverlink",
        "@accessibility//jar",
        "@dexmaker//jar",
        "@dexmaker_mockito//jar",
        "@guava//jar",
        "@hamcrest//jar",
        "@javax_inject//jar",
        "@jsr305//jar",
        "@junit//jar",
        "@mockito//jar",
        "@objenesis//jar",
        "@truth//jar",
    ],
)

android_binary(
    name = "espresso_contrib_tests",
    testonly = 1,
    instruments = "//testapps/ui_testapp/java/androidx/test/ui/app:testapp",
    manifest = "AndroidManifest.xml",
    deps = [":espresso_contrib_test_lib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "espresso_contrib_tests_%s" % api_level,
    size = "large",
    args = [
        "--clear_package_data",
        "--install_test_services=True",  # Required for Bazel 0.11, can be removed in 0.12
    ],
    target_device = "//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % (api_level),
    test_app = ":espresso_contrib_tests",
) for api_level in API_LEVELS]
