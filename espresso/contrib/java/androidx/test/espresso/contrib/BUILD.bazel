load("//build_extensions:release.bzl", "axt_release_lib")
load("//build_extensions:maven_repo.bzl", "maven_artifact")
load("//build_extensions:axt_versions.bzl", "ANDROIDX_VERSION", "ESPRESSO_VERSION", "GOOGLE_MATERIAL_VERSION")
load("//build_extensions:combine_jars.bzl", "combine_jars")

# Description:
#   Contributed / non-core functionality of Espresso. Tools which add on to
#   Espresso belong here.
licenses(["notice"])  # Apache License 2.0

package(
    default_visibility = ["//:__subpackages__"],
)

android_library(
    name = "resources",
    manifest = "AndroidManifest.xml",
    resource_files = glob(["res/**"]),
)

android_library(
    name = "counting_idling_resource",
    visibility = ["//visibility:public"],
    exports = [
        "//espresso/idling_resource/java/androidx/test/espresso:counting_idling_resource",
    ],
)

android_library(
    name = "scheduled_thread_pool_idling_resource",
    visibility = ["//visibility:public"],
    exports = [
        "//espresso/idling_resource/concurrent/java/androidx/test/espresso/idling/concurrent:scheduled_thread_pool_idling_resource",
    ],
)

android_library(
    name = "thread_pool_idling_resource",
    visibility = ["//visibility:public"],
    exports = [
        "//espresso/idling_resource/concurrent/java/androidx/test/espresso/idling/concurrent:thread_pool_idling_resource",
    ],
)

android_library(
    name = "picker_actions",
    srcs = ["PickerActions.java"],
    visibility = ["//visibility:public"],
    deps = [
        "//:androidx_annotation",
        "//:androidx_core",
        "//:androidx_legacy_support_core_ui",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso:framework",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "@maven//:com_google_code_findbugs_jsr305",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "recycler_view",
    srcs = ["RecyclerViewActions.java"],
    visibility = ["//visibility:public"],
    deps = [
        ":guava_checks",
        "//:androidx_annotation",
        "//:androidx_core",
        "//:androidx_legacy_support_core_ui",
        "//:androidx_recyclerview",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso:framework",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "@maven//:com_google_code_findbugs_jsr305",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "drawer_actions",
    testonly = 1,
    srcs = [
        "DrawerActions.java",
        "DrawerMatchers.java",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":resources",
        "//:androidx_annotation",
        "//:androidx_core",
        "//:androidx_drawerlayout",
        "//:androidx_legacy_support_core_ui",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "activity_result_matchers",
    srcs = [
        "ActivityResultMatchers.java",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "accessibility_checks",
    testonly = 1,
    srcs = [
        "AccessibilityChecks.java",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":guava_checks",
        "//espresso/core/java/androidx/test/espresso",
        "//runner/android_junit_runner",
        "@maven//:com_google_android_apps_common_testing_accessibility_framework_accessibility_test_framework",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "design_lib_actions",
    testonly = 1,
    srcs = ["NavigationViewActions.java"],
    visibility = ["//visibility:public"],
    deps = [
        "//:androidx_annotation",
        "//:androidx_core",
        "//:androidx_drawerlayout",
        "//:androidx_legacy_support_core_ui",
        "//:androidx_recyclerview",
        "//:google_android_material",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso:framework",
        "//espresso/core/java/androidx/test/espresso:interface",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "@maven//:com_google_code_findbugs_jsr305",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "view_pager_actions",
    testonly = 1,
    srcs = ["ViewPagerActions.java"],
    visibility = ["//visibility:public"],
    deps = [
        "//:androidx_annotation",
        "//:androidx_core",
        "//:androidx_legacy_support_core_ui",
        "//:androidx_viewpager",
        "//espresso/core/java/androidx/test/espresso",
        "//espresso/core/java/androidx/test/espresso/action",
        "//espresso/core/java/androidx/test/espresso/matcher",
        "@maven//:org_hamcrest_hamcrest_all",
    ],
)

android_library(
    name = "guava_checks",
    srcs = ["Checks.java"],
    deps = ["@maven//:com_google_code_findbugs_jsr305"],
)

android_library(
    name = "espresso_contrib_release_lib",
    testonly = 1,
    exports = [
        ":accessibility_checks",
        ":activity_result_matchers",
        ":design_lib_actions",
        ":drawer_actions",
        ":guava_checks",
        ":picker_actions",
        ":recycler_view",
        ":view_pager_actions",
    ],
)

axt_release_lib(
    name = "espresso_contrib_release",
    keep_spec = "androidx/test/espresso/contrib",
    multidex = "native",
    remove_spec = "androidx/test/espresso/contrib/R[$$\\.]",
    resource_files = glob(["res/**"]),
    deps = [
        ":espresso_contrib_release_lib",
    ],
)

filegroup(
    name = "contrib_src",
    srcs = [
        ":libaccessibility_checks-src.jar",
        ":libactivity_result_matchers-src.jar",
        ":libcounting_idling_resource-src.jar",
        ":libdesign_lib_actions-src.jar",
        ":libdrawer_actions-src.jar",
        ":libpicker_actions-src.jar",
        ":librecycler_view-src.jar",
        ":libview_pager_actions-src.jar",
    ],
)

combine_jars(
    name = "contrib_src_jars",
    srcs = [
        ":contrib_src",
    ],
)

maven_artifact(
    name = "espresso_contrib_maven_artifact",
    src = ":espresso_contrib_release.aar",
    artifact_deps = [
        "androidx.test.espresso:espresso-core:%s" % ESPRESSO_VERSION,
        "androidx.test.espresso:espresso-idling-resource:%s" % ESPRESSO_VERSION,
        "com.google.android.material:material:%s" % GOOGLE_MATERIAL_VERSION,
        "androidx.recyclerview:recyclerview:%s" % ANDROIDX_VERSION,
        "androidx.drawerlayout:drawerlayout:%s" % ANDROIDX_VERSION,
        "androidx.viewpager:viewpager:%s" % ANDROIDX_VERSION,
        "androidx.core:core:%s" % ANDROIDX_VERSION,
        "androidx.legacy:legacy-support-core-utils:%s" % ANDROIDX_VERSION,
        "com.google.android.apps.common.testing.accessibility.framework:accessibility-test-framework:2.0",
    ],
    artifact_id = "espresso-contrib",
    group_id = "androidx.test.espresso",
    last_updated = "20170622000000",
    src_jar = ":contrib_src_jars.jar",
    version = "%s" % ESPRESSO_VERSION,
)
