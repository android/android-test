load("//build_extensions:release.bzl", "axt_release_lib")
load("//build_extensions:maven_repo.bzl", "maven_artifact")
load("//build_extensions:axt_versions.bzl", "ANDROIDX_ANNOTATION_VERSION", "ANDROIDX_CONCURRENT_VERSION", "ANDROIDX_CORE_VERSION", "ANDROIDX_TRUTH_VERSION", "ANNOTATION_VERSION", "CORE_VERSION", "GUAVA_VERSION", "TRUTH_VERSION")

# Description: Build rules for building androidx.test.truth from source
licenses(["notice"])  # Apache License 2.0

package(
    default_visibility = [
        "//visibility:public",
    ],
)

android_library(
    name = "truth",
    testonly = 1,
    srcs = glob(
        ["**/*.java"],
    ),
    manifest = "AndroidManifest.xml",
    visibility = ["//visibility:public"],
    deps = [
        "//:androidx_annotation",
        "//:androidx_concurrent_futures",
        "//:androidx_core",
        "//annotation",
        "//core",
        "@maven//:com_google_guava_guava",
        "@maven//:com_google_truth_truth",
    ],
)

# group of targets to use to produce release binary + docs
android_library(
    name = "truth_release_lib",
    testonly = 1,
    exports = [
        ":truth",
    ],
)

# Generate rules for the release artifacts. This generates three targets
# genrule output: truth_release_no_deps.jar and truth_release.aar
# a java_library target libtruth_release_no_deps
axt_release_lib(
    name = "truth_release",
    keep_spec = "androidx/test/ext/truth",
    remove_spec = "androidx/test/ext/truth/R[$$\\.]",
    deps = [
        ":truth_release_lib",
    ],
)

maven_artifact(
    name = "truth_maven_artifact",
    src = ":truth_release.aar",
    artifact_deps = [
        "androidx.test:core:%s" % CORE_VERSION,
        "com.google.guava:guava:%s" % GUAVA_VERSION,
        "com.google.truth:truth:%s" % TRUTH_VERSION,
        "androidx.annotation:annotation:%s" % ANDROIDX_ANNOTATION_VERSION,
        "androidx.concurrent:concurrent-futures:%s" % ANDROIDX_CONCURRENT_VERSION,
        "androidx.core:core:%s" % ANDROIDX_CORE_VERSION,
        "androidx.test:annotation:%s" % ANNOTATION_VERSION,
    ],
    artifact_id = "truth",
    group_id = "androidx.test.ext",
    last_updated = "20180321000000",
    src_jar = ":libtruth-src.jar",
    version = "%s" % ANDROIDX_TRUTH_VERSION,
)
