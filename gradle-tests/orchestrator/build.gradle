plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "17"
    }
}

android {
    namespace 'androidx.test.gradletests.orchestrator'
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        // test with whitespace requires min sdk 30
        minSdk 30
        targetSdk rootProject.ext.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
        managedDevices {
            devices {
                // run with ../gradlew  nexusOneDebugAndroidTest
                nexusOne(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = rootProject.ext.emulatorApi
                    // Also use the AOSP Automated Test Device image for better emulator performance
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
}

dependencies {
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.runner
    androidTestImplementation libs.monitor
    androidTestImplementation libs.ext.truth
    androidTestUtil libs.orchestrator
}
